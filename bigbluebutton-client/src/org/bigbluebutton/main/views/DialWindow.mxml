<?xml version="1.0" encoding="utf-8"?>
<!--

BigBlueButton open source conferencing system - http://www.bigbluebutton.org/

Copyright (c) 2012 BigBlueButton Inc. and by respective authors (see below).

This program is free software; you can redistribute it and/or modify it under the
terms of the GNU Lesser General Public License as published by the Free Software
Foundation; either version 3.0 of the License, or (at your option) any later
version.

BigBlueButton is distributed in the hope that it will be useful, but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.

You should have received a copy of the GNU Lesser General Public License along
with BigBlueButton; if not, see <http://www.gnu.org/licenses/>.

-->
<mx:TitleWindow xmlns="flexlib.mdi.containers.*" 
           xmlns:mx="http://www.adobe.com/2006/mxml"
           xmlns:mate="http://mate.asfusion.com/"
           minWidth="250" 
           showCloseButton="false"
           styleName="dialWindowStyle"
           creationComplete="onCreationComplete()">

        <mate:Listener type="{LocaleChangeEvent.LOCALE_CHANGED}" method="localeChanged"/>
        <mate:Listener type="{VoiceConfEvent.DIALING}" method="statusChanged"/>
        <mate:Listener type="{VoiceConfEvent.HANGINGUP}" method="statusChanged"/>
        
        <mx:Script>
                <![CDATA[
        	import mx.events.ItemClickEvent;
            import mx.managers.PopUpManager;
            import mx.core.UIComponent;
                        
			import flash.events.Event;
			import com.asfusion.mate.events.Dispatcher;
			import flexlib.mdi.events.MDIWindowEvent;
			
			import flexlib.controls.textClasses.StringBoundaries;
			
			import mx.collections.ArrayCollection;
			import mx.collections.ArrayList;
			
			import org.bigbluebutton.common.LogUtil;
			import org.bigbluebutton.common.events.LocaleChangeEvent;
			import org.bigbluebutton.util.i18n.ResourceUtil;
			import org.bigbluebutton.core.events.VoiceConfEvent;
			import org.bigbluebutton.core.BBB;
			
			private var options:Array;
			private var params:Array;
			private var meetingTitle:String;
			
			[Bindable]
			private var statusString:String;
			
			[Bindable]
			private var isDial:Boolean;
                        
			private function onCreationComplete():void {
		        this.x = (this.parent.width - this.width) / 2;
                this.y = (this.parent.height - this.height) / 2;
                
                options = new Array();
                params = new Array();
                
                updateDialWindow();
			}
			
			private function localeChanged(e:Event):void {
		        updateDialWindow();
			}
			
			private function statusChanged(e:Event):void {
			    var event:VoiceConfEvent = VoiceConfEvent(e);
			    if(event.dialState == "RINGING") {
			        statusString = ResourceUtil.getInstance().getString('bbb.dialWindow.state.ringing');
		        }
			    else if(event.dialState == "EARLY") {
			        statusString = ResourceUtil.getInstance().getString('bbb.dialWindow.state.early');
		        }
		        else if(event.dialState == "ACTIVE") {
		            statusString = ResourceUtil.getInstance().getString('bbb.dialWindow.state.active');
		            clearDial(true, true);
		            close();
		        }
		        else if(event.dialState == "HANGUP") {
		            if(event.dialHangupCause == "USER_BUSY") {
		                statusString = 
		                        ResourceUtil.getInstance().getString('bbb.dialWindow.hangupcause.user_busy');
		            }
		            else if(event.dialHangupCause == "NO_USER_RESPONSE") {
                        statusString = 
                                ResourceUtil.getInstance().getString('bbb.dialWindow.hangupcause.no_user_response');
                    }
                    else if(event.dialHangupCause == "NO_ANSWER") {
                        statusString = 
                                ResourceUtil.getInstance().getString('bbb.dialWindow.hangupcause.no_answer');
                    }
                    else if(event.dialHangupCause == "CALL_REJECTED") {
                        statusString = 
                                ResourceUtil.getInstance().getString('bbb.dialWindow.hangupcause.call_rejected');
                    }
                    else if(event.dialHangupCause == "DESTINATION_OUT_OF_ORDER") {
                        statusString = 
                                ResourceUtil.getInstance().getString('bbb.dialWindow.hangupcause.destination_out_of_order');
                    }
                    else if(event.dialHangupCause == "NORMAL_CLEARING") {
                        statusString = 
                            ResourceUtil.getInstance().getString('bbb.dialWindow.hangupcause.normal_clearing');
                        dialCancelBtn.enabled = true;
                        clearDial(false, false);
                    }
                    else {
			            statusString = event.dialHangupCause;
		            }
		        }
		        else if(event.dialState == "DOWN") {
		            cancelDial(false, false);
                    clearDial(false, false);
			        /*statusString = ResourceUtil.getInstance().getString('bbb.dialWindow.state.down');*/
		        }
		        else {
		            statusString = ResourceUtil.getInstance().getString('bbb.dialWindow.state.unknown');
		            clearDial(false, true);
		        }
		    }
			
			private function updateDialWindow():void {
		        sipLabel.text = ResourceUtil.getInstance().getString('bbb.dialWindow.sip.label');
		        dialCancelBtn.label = ResourceUtil.getInstance().getString('bbb.dialWindow.dialBtn');
		        closeBtn.label = ResourceUtil.getInstance().getString('bbb.dialWindow.closeBtn');
                statusString = "";
		        destination.setFocus();
		        isDial = true;
			}
			
			private function dialOrCancel():void {
			    if(isDial)
			    {
			        configureDial();
			    }
                else
                {
                    cancelDial(true, true);
                }
			}
			
			private function configureDial():void {
                if(destination.text == "") {
                    statusString = "destination is empty."
                }
                else {
    		        meetingTitle = BBB.initUserConfigManager().getMeetingTitle();
    				options["origination_caller_id_name"] = meetingTitle;
    				params["destination"] = destination.text;
    				
    				destination.editable = false;
    				dialCancelBtn.label = ResourceUtil.getInstance().getString('bbb.dialWindow.cancelBtn');
    				closeBtn.enabled = false;
    				isDial = false;
    				
    				dial(options, params);
                }
			}
			
			private function dial(options:Array, params:Array):void {
				var e:VoiceConfEvent = new VoiceConfEvent(VoiceConfEvent.DIAL);
				e.dialOptions = options;
				e.dialParams = params;
				dispatchEvent(e);
			}
			
			private function cancelDial(clearStatus:Boolean, enableDial:Boolean):void {
			    destination.editable = true;
			    destination.setFocus();
                dialCancelBtn.label = ResourceUtil.getInstance().getString('bbb.dialWindow.dialBtn');
                closeBtn.enabled = true;
                isDial = true;
			    
			    if(clearStatus) {
			        statusString = "";
			    }

                if(enableDial) {
                    dialCancelBtn.enabled = false;
                }

	            var e:VoiceConfEvent = new VoiceConfEvent(VoiceConfEvent.CANCEL_DIAL);
	            e.cancelDialIdName = meetingTitle;
	            e.cancelDialDestination = "sofia/external/" + destination.text;
	            dispatchEvent(e);
			}
			
			private function clearDial(clearStatus:Boolean, 
		                                clearDst:Boolean):void {
			    destination.editable = true;
			    dialCancelBtn.label = ResourceUtil.getInstance().getString('bbb.dialWindow.dialBtn');
			    closeBtn.enabled = true;
			    isDial = true;
			    destination.setFocus();
			    
			    if(clearStatus) {
			        statusString = "";
			    }
			    
			    var dst:String = destination.text;
			    
			    if(clearDst) {
			        destination.text = "";
		        }
		        
			    var e:VoiceConfEvent = new VoiceConfEvent(VoiceConfEvent.CLEAR_DIAL);
	            e.cancelDialIdName = meetingTitle;
	            e.cancelDialDestination = "sofia/external/" + dst;
	            dispatchEvent(e);
			}
			
			private function close():void {
                PopUpManager.removePopUp(this);
            }
                ]]>
        </mx:Script>

        <mx:VBox id="dialDisplay" width="100%" height="100%" paddingBottom="5" paddingLeft="5" paddingRight="5" paddingTop="5">
            <mx:HBox width="100%" horizontalAlign="left" paddingTop="5" paddingLeft="5" paddingRight="5" paddingBottom="5">
                <mx:Label text="{ResourceUtil.getInstance().getString('bbb.dialWindow.windowTitle')}" styleName="webcamSettingsWindowTitleStyle"/>
            </mx:HBox>

            <mx:HRule width="100%"/>

            <mx:HBox paddingTop="20" paddingBottom="10" paddingLeft="5" paddingRight="5" verticalAlign="middle">
                        <mx:Label id="sipLabel" styleName="dialSipLabelStyle"/>
                        <mx:TextInput id="destination" width="210" text=""/>
                        <mx:HBox paddingLeft="5">
                            <mx:Button id="dialCancelBtn" width="80" click="dialOrCancel()"/>
                        </mx:HBox>
            </mx:HBox>

            <mx:Text id="statusTxt" text="{statusString}" paddingLeft="{destination.x}"/>

            <mx:HRule width="100%"/>

            <mx:HBox width="100%" height="10%" horizontalAlign="right" horizontalGap="13" paddingRight="5" paddingBottom="5" paddingTop="5">
                <mx:Button id="closeBtn" width="80" click="close()"/>
            </mx:HBox>
        </mx:VBox>
</mx:TitleWindow>